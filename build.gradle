buildscript {
    ext.kotlin_version = '1.4.32'
    ext.flipper_version = '0.91.2'
    ext.retrofit_version = '2.9.0'
    ext.protobuf_version = '3.17.1'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
    	classpath "com.android.tools.build:gradle:4.2.1"
    	classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.16'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.15.1'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.32'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
    id 'io.gitlab.arturbosch.detekt' version '1.17.1'
}

tasks.detekt.jvmTarget = "1.8"
detekt {
    buildUponDefaultConfig = true // preconfigure defaults
    allRules = true // activate all available (even unstable) rules.

    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
        xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    project.ext {
        desktopIP="\"${getLocalIPv4()[0]}\""
    }
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

def getLocalIPv4() {
    def ip4s = []
    NetworkInterface.getNetworkInterfaces()
        .findAll { it.isUp() && !it.isLoopback() && !it.isVirtual() }
        .each {
            it.getInetAddresses()
                .findAll { !it.isLoopbackAddress() && it instanceof Inet4Address }
                .each { ip4s << it.getHostAddress() }
        }
    return ip4s
}

task addKtLintStyle(type: JavaExec, group: "formatting") {
    description = "Adds The KtLint Style To Your IDE"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--android", "applyToIDEA", "-y"
    //Point to the root directory because this task needs access to the .idea directory
    workingDir(getRootDir())
}
